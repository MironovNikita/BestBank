plugins {
	id 'java'
	id 'org.springframework.boot' version("${springBootVersion}") apply(false)
	id 'io.spring.dependency-management' version("${dependencyManagementVersion}") apply(false)
}

allprojects {
	repositories {
		mavenCentral()
	}
}

description = 'Bank App with microservice architecture'

subprojects {
	group = 'com.bank'
	version = '0.0.1-SNAPSHOT'

	apply plugin: 'java'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	java {
		toolchain {
			languageVersion = JavaLanguageVersion.of(21)
		}
	}

	dependencyManagement {
		imports {
			mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
		}
	}

	tasks.withType(JavaCompile).configureEach {
		options.compilerArgs << '-parameters'
	}

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	dependencies {
		implementation 'org.springframework.boot:spring-boot-starter'
		implementation 'org.springframework.boot:spring-boot-starter-validation'
		implementation 'org.springframework.boot:spring-boot-starter-webflux'
		implementation 'org.springframework.boot:spring-boot-starter-actuator'

		compileOnly 'org.projectlombok:lombok'
		annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

		testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
		testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

		testImplementation 'org.springframework.boot:spring-boot-starter-test'

		testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	}

	tasks.withType(Test).tap {
		configureEach {
			useJUnitPlatform()

			jvmArgs += ["-XX:+EnableDynamicAgentLoading"]

			testLogging {
				events("passed", "skipped", "failed")
				exceptionFormat = "full"

				lifecycle {
					showStandardStreams = true
				}
			}

			afterSuite { desc, result ->
				if (!desc.parent) {
					println "--------------------------------------------------"
					println "Тестов всего: ${result.testCount}"
					println "Успешно:      ${result.successfulTestCount}"
					println "Провалено:    ${result.failedTestCount}"
					println "Пропущено:    ${result.skippedTestCount}"
					println "Результат:    ${result.resultType}"
					println "--------------------------------------------------"
				}
			}
		}
	}
}
